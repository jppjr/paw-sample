#region Prolog


#################################################################
## IBM Confidential
##
## OCO Source Materials
##
## BI and PM: pmpsvc
##
## (C) Copyright IBM Corp. 2014
##
## The source code for this program is not published or otherwise
## divested of its trade secrets, irrespective of what has been
## deposited with the U.S. Copyright Office.
#################################################################


#*** Add element 'EnableElementSecurityOnApproval' to dimension '}tp_config'

cConfigDim = '}tp_config';
If (DimensionExists(cConfigDim) = 0);
	ProcessError;
EndIf;

cEnableElementSecurity = 'EnableElementSecurityOnApproval';
If (DIMIX(cConfigDim, cEnableElementSecurity)=0);
	DimensionElementInsert(cConfigDim, '', cEnableElementSecurity,'S');
EndIf;


#*** Add attribute 'SecurityMethod' to dimension '}tp_applications'

cApplicationsDim = '}tp_applications';
If (DimensionExists(cApplicationsDim) = 0);
	ProcessError;
EndIf;

cApplicationsAttributesDim = 'ElementAttributes_}' | cApplicationsDim;
cSecurityMethod = 'SecurityMethod';
If (DIMIX(cApplicationsAttributesDim, cSecurityMethod)=0);
	AttrInsert(cApplicationsDim, '', cSecurityMethod,'S');
EndIf;


#*** Add jobs dimension

cJobsDim = '}tp_jobs';

If (DimensionExists(cJobsDim) = 0);
	DimensionCreate(cJobsDim);
EndIf;

#*** Add jobs info dimension

cJobInfoDim = '}tp_job_info';

cJobAppId = 'ApplicationId';
cJobAppName = 'ApplicationName';
cJobType = 'JobType';
cJobOwner = 'Owner';
cJobOwnerId = 'OwnerId';
cJobStartDate = 'StartDate';
cJobEndDate = 'EndDate';
cJobStatus = 'JobStatus';

If (DimensionExists(cJobInfoDim) = 0);

	DimensionCreate(cJobInfoDim);
	
	DimensionElementInsert(cJobInfoDim, '', cJobAppId, 'S');
	DimensionElementInsert(cJobInfoDim, '', cJobAppName, 'S');
	DimensionElementInsert(cJobInfoDim, '', cJobType, 'S');
	DimensionElementInsert(cJobInfoDim, '', cJobOwner, 'S');
	DimensionElementInsert(cJobInfoDim, '', cJobOwnerId, 'S');
	DimensionElementInsert(cJobInfoDim, '', cJobStartDate, 'S');
	DimensionElementInsert(cJobInfoDim, '', cJobEndDate, 'S');
	DimensionElementInsert(cJobInfoDim, '', cJobStatus, 'S');
EndIf;

#*** create cube to store application jobs 

cApplicationJobCube = '}tp_application_jobs';
If (CubeExists(cApplicationJobCube) = 0);
	CubeCreate(cApplicationJobCube, cJobsDim, cJobInfoDim);
	CubeSetLogChanges(cApplicationJobCube, 1);
EndIf;


#*** Create task navigation dimension
cTaskNavDimension = '}tp_task_navigations';
cNavElemField = 'NavigationElement';
If (DimensionExists(cTaskNavDimension) = 0);
	DimensionCreate(cTaskNavDimension);
	DimensionElementInsert(cTaskNavDimension, '',cNavElemField , 'S');
EndIf;


#*** Create artifacts for central applications

cRootPermissionsDim = '}tp_root_permissions';

cOffline = 'OFFLINE';
cOwnership = 'OWN';

If (DimensionExists(cRootPermissionsDim) = 0);
	DimensionCreate(cRootPermissionsDim);
	
	DimensionElementInsert(cRootPermissionsDim, '',cOffline,'S');
	DimensionElementInsert(cRootPermissionsDim, '', cOwnership, 'S');
	SubsetCreate(cRootPermissionsDim, 'Default');
	SubsetIsAllSet(cRootPermissionsDim, 'Default', 1);
EndIf;

cRootPermissionsCube = '}tp_application_root_permissions';
cCellSecurityRootPermissionsCube = '}CellSecurity_' | cRootPermissionsCube;

If (CubeExists(cRootPermissionsCube) = 0);
	CubeCreate(cRootPermissionsCube, cApplicationsDim, cRootPermissionsDim);
	CubeSetLogChanges(cRootPermissionsCube, 1);	
EndIf;

If (CubeExists(cCellSecurityRootPermissionsCube) = 0);
	CubeCreate(cCellSecurityRootPermissionsCube, cApplicationsDim, cRootPermissionsDim, '}Groups');
	CubeSetLogChanges(cCellSecurityRootPermissionsCube, 1);
EndIf;
#endregion
#region Epilog


#################################################################
## IBM Confidential
##
## OCO Source Materials
##
## BI and PM: pmpsvc
##
## (C) Copyright IBM Corp. 2014
##
## The source code for this program is not published or otherwise
## divested of its trade secrets, irrespective of what has been
## deposited with the U.S. Copyright Office.
#################################################################


#*** Do not enforce element security on approval dimension for upgrade
If (ATTRS(cConfigDim, cEnableElementSecurity, 'String Value') @= '');
	AttrPutS('Y', cConfigDim, cEnableElementSecurity, 'String Value');
EndIf;
#endregion