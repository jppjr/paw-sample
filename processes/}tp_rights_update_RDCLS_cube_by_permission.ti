#region Prolog


#################################################################
## IBM Confidential
##
## OCO Source Materials
##
## BI and PM: pmpsvc
##
## (C) Copyright IBM Corp. 2008, 2009, 2010
##
## The source code for this program is not published or otherwise
## divested of its trade secrets, irrespective of what has been
## deposited with the U.S. Copyright Office.
#################################################################

#*** Log File Name
cConfigDim = '}tp_config';
If (DimensionExists(cConfigDim) = 1);
	cGenerateLog = ATTRS(cConfigDim, 'Generate TI Log', 'String Value');
Else;
	cGenerateLog = 'N';
EndIf;

cTM1Process = GetProcessName();
StringGlobalVariable('gPrologLog');
StringGlobalVariable('gEpilogLog');
StringGlobalVariable('gDataLog');

IF (cGenerateLog @= 'Y' % cGenerateLog @= 'T');
vReturnValue = ExecuteProcess('}tp_get_log_file_names', 'pExecutionId', pExecutionId,
'pProcess', cTM1Process, 'pControl', 'Y');
If (vReturnValue <> ProcessExitNormal());
	ProcessError;
EndIf;
Endif;

cPrologLog = gPrologLog;
cEpilogLog = gEpilogLog;
cDataLog = gDataLog;
cTM1Log = cPrologLog;

#*** Log Parameters

If (cGenerateLog @= 'Y');
	TextOutput(cTM1Log, TIMST(NOW, '\Y-\m-\d \h:\i:\s'), 'Parameters:',pExecutionId, pAppId, pCube,pApprovalDim, pApprovalSubset, pVersionDim, pVersionSlicesWrite, pVersionSlicesRead);
EndIf;

#***
vCellSecurityPermissionCube = '}CellSecurity_}tp_application_permission}' | pAppId;
cIMRDCLSCube = '}tp_intermediate_RDCLS}' | pCube;
cCellSecurityCube = '}CellSecurity_' | pCube;
cAllApplications = 'all_applications';
cIMApplicationDim = '}tp_intermediate_security_applications';
cIMMeasures = '}tp_intermediate_security_measures';
vIncrGroupDim = 'tp_incr_temp_groups_' | pAppId;

#***
cGroupsDim = '}Groups';
IF (pIncremental @= 'Y');
	cGroupsDim = vIncrGroupDim;
Endif;

#***
versionIndex = 0;
approvalIndex = 0;
dimensionIndex = 1;
While (dimensionIndex > 0 ); 
	cCubeDimensionName = TABDIM(pCube, dimensionIndex);
	If (cCubeDimensionName @= '');
		dimensionIndex = -1;
	Else;
		IF (cCubeDimensionName @=pVersionDim);
			versionIndex = dimensionIndex;
		Endif;
				
		IF (cCubeDimensionName @= pApprovalDim );
			approvalIndex =dimensionIndex ;
		Endif;

	EndIf;

	dimensionIndex = dimensionIndex + 1;
End;

IF (versionIndex < approvalIndex);
	versionFirst = 'Y';
Endif;

#*Update Intermediate cell security cube
IF (pUpdateIntermediateSecurity @= 'Y');
	#create an application subset that contains this application Id only
	vTempAppSubset = 'temp_app_' | pAppId;
	IF (SubsetExists(cIMApplicationDim, vTempAppSubset)>0);
		SubsetDestroy(cIMApplicationDim, vTempAppSubset);
	Endif;
	SubsetCreate(cIMApplicationDim, vTempAppSubset);
	SubsetElementInsert(cIMApplicationDim, vTempAppSubset, pAppId, 1);

	IF (pIncremental @<> 'Y');
		vAllView = 'tp_temp_RDCLS_view_' | pExecutionId;
		ViewCreate(cIMRDCLSCube, vAllView);
		ViewTitleDimensionSet(cIMRDCLSCube, vAllView, cIMApplicationDim);
		ViewSubsetAssign(cIMRDCLSCube, vAllView, cIMApplicationDim, vTempAppSubset);
		ViewZeroOut(cIMRDCLSCube, vAllView);
		ViewDestroy(cIMRDCLSCube, vAllView);
	Endif;

	vTotalGroups = DIMSIZ(cGroupsDim);
	vGroupLooper = 1;
	While (vGroupLooper <= vTotalGroups);
		vGroup = DIMNM(cGroupsDim, vGroupLooper);

		#zero out cell security cube based on this group and this application only
		IF (pIncremental @= 'Y');
			#create a group subset that contains this group only
			vTempGroupSubset = 'temp_group_' | pAppId;
			vGroupsControlDim = '}Groups';
			IF (SubsetExists(vGroupsControlDim, vTempGroupSubset)>0);
				SubsetDestroy(vGroupsControlDim, vTempGroupSubset);
			Endif;
			SubsetCreate(vGroupsControlDim, vTempGroupSubset);
			SubsetElementInsert(vGroupsControlDim, vTempGroupSubset, vGroup, 1);

			vGroupView = 'tp_temp_group_view_'  | pExecutionId;
			ViewCreate(cIMRDCLSCube, vGroupView);
			ViewTitleDimensionSet(cIMRDCLSCube, vGroupView, vGroupsControlDim);
			ViewTitleDimensionSet(cIMRDCLSCube, vGroupView, cIMApplicationDim);
			ViewSubsetAssign(cIMRDCLSCube, vGroupView, vGroupsControlDim, vTempGroupSubset );
			ViewSubsetAssign(cIMRDCLSCube, vGroupView, cIMApplicationDim, vTempAppSubset);
			ViewZeroOut(cIMRDCLSCube, vGroupView);
			ViewDestroy(cIMRDCLSCube, vGroupView);

			IF (SubsetExists(vGroupsControlDim, vTempGroupSubset)>0);
				SubsetDestroy(vGroupsControlDim, vTempGroupSubset);
			Endif;
		Endif;

		vTotalNodes = SubsetGetSize(pApprovalDim, pApprovalSubset);
		vNodeLooper = 1;
		While (vNodeLooper <= vTotalNodes);
			vApprovalNode = SubsetGetElementName(pApprovalDim, pApprovalSubset, vNodeLooper);
			vView = CellGetS(vCellSecurityPermissionCube, vApprovalNode, 'VIEW', vGroup);
			vEdit = CellGetS(vCellSecurityPermissionCube, vApprovalNode, 'EDIT', vGroup);
			vNodeSecurity = '';
			vVersionSecurity = '';
			vFinalSecurity = '';
			IF (vView @= 'READ');
				IF (DType(pApprovalDim, vApprovalNode) @='C');
					vNodeSecurity = 'WRITE';
				Else;
					vNodeSecurity = 'READ';
				Endif;
			Endif;

			IF (vEdit @= 'READ');
				vNodeSecurity = 'WRITE';
			Endif;
							
			IF (vNodeSecurity @<> '');

			#***readable version slices
			versionSeparater = '|';
			vPosVersion = 0;
			vStringToScan = pVersionSlicesRead;
			IF (pVersionSlicesRead @<> '');
				vPosVersion = SCAN(versionSeparater, vStringToScan);
			Else;
				#Even no readable version slices are defined, we still need to apply security
				#based on approval dimension
				vPosVersion = 1;
			Endif;

			While (vPosVersion >0);
				vVersionSlice  = SUBST(vStringToScan, 1, vPosVersion-1);
				vStringToScan = SUBST(vStringToScan, vPosVersion +1, LONG(vStringToScan)-vPosVersion);
				vPosVersion = SCAN(versionSeparater, vStringToScan);

				IF (versionIndex >0);
					IF (vVersionSlice @<> '' );
						IF (DIMIX(pVersionDim, vVersionSlice)=0);
							vReturnValue = ExecuteProcess('}tp_error_update_error_cube',
								'pGuid', pExecutionId,
								'pProcess', cTM1Process,
								'pErrorCode', 'TI_READABLE_SLICE_NOT_EXISTS',
								'pErrorDetails', pVersionDim  | '.' | vVersionSlice | ', ' |  pAppId,
								'pControl', 'Y');
	
								ProcessError;
						Endif;

						IF (approvalIndex >0);
							IF (versionFirst @= 'Y');
								CellPutS('READ', cIMRDCLSCube, vVersionSlice, vApprovalNode,pAppId, vGroup,'Rights' );
							Else;
								CellPutS('READ', cIMRDCLSCube, vApprovalNode, vVersionSlice,pAppId, vGroup,'Rights' );
							Endif;
						Else;
							CellPutS('READ', cIMRDCLSCube, vVersionSlice, pAppId, vGroup,'Rights' );	
						Endif;
					Endif;

				ElseIf (versionIndex = 0 & approvalIndex >0);
					CellPutS('READ', cIMRDCLSCube, vApprovalNode, pAppId, vGroup,'Rights' );
				Endif;

			End;

			#***writable version slices
			versionSeparater = '|';
			vPosVersion = 0;
			vStringToScan = pVersionSlicesWrite;
			IF (pVersionSlicesWrite @<> '');
				vPosVersion = SCAN(versionSeparater, vStringToScan);
			Else;
				#Even no writable version slices are defined, we still need to apply security
				#based on approval dimension
				vPosVersion = 1;
			Endif;

			While (vPosVersion >0);
				vVersionSlice  = SUBST(vStringToScan, 1, vPosVersion-1);
				vStringToScan = SUBST(vStringToScan, vPosVersion +1, LONG(vStringToScan)-vPosVersion);
				vPosVersion = SCAN(versionSeparater, vStringToScan);
				IF (vNodeSecurity @= 'READ');
					vFinalSecurity = 'READ';
				Elseif (vNodeSecurity @= 'WRITE');
					vFinalSecurity = 'WRITE';
				Endif;
	
				IF (versionIndex >0);
					IF (vVersionSlice @<>'');
						IF (DIMIX(pVersionDim, vVersionSlice)=0);
							vReturnValue = ExecuteProcess('}tp_error_update_error_cube',
								'pGuid', pExecutionId,
								'pProcess', cTM1Process,
								'pErrorCode', 'TI_WRITABLE_SLICE_NOT_EXISTS',
								'pErrorDetails', pVersionDim  | '.' | vVersionSlice | ', ' |  pAppId,
								'pControl', 'Y');
	
								ProcessError;
						Endif;

						IF (approvalIndex >0);
							IF (versionFirst @= 'Y');
								CellPutS(vFinalSecurity, cIMRDCLSCube, vVersionSlice, vApprovalNode,pAppId, vGroup,'Rights' );
							Else;
								CellPutS(vFinalSecurity, cIMRDCLSCube, vApprovalNode, vVersionSlice,pAppId, vGroup,'Rights' );
							Endif;
						Else;
							CellPutS(vFinalSecurity, cIMRDCLSCube, vVersionSlice, pAppId, vGroup,'Rights' );
						Endif;
					Endif;

				ElseIf (versionIndex = 0 & approvalIndex >0);
					CellPutS(vFinalSecurity, cIMRDCLSCube, vApprovalNode, pAppId, vGroup,'Rights' );
				Endif;
			End;
			Endif;

			vNodeLooper = vNodeLooper  +1;
		End;
		vGroupLooper = vGroupLooper +1;
	End;

	IF (SubsetExists(cIMApplicationDim, vTempAppSubset)>0);
		SubsetDestroy(cIMApplicationDim, vTempAppSubset);
	Endif;
Endif;
#endregion
#region Epilog


#################################################################
## IBM Confidential
##
## OCO Source Materials
##
## BI and PM: pmpsvc
##
## (C) Copyright IBM Corp. 2008, 2009, 2010
##
## The source code for this program is not published or otherwise
## divested of its trade secrets, irrespective of what has been
## deposited with the U.S. Copyright Office.
#################################################################

#*Update RD cell security cubes

#Create a subset that contains StaticRights only
vTempStaticRightsSubset = 'temp_static_rights_' | pAppId;
IF (SubsetExists(cIMMeasures, vTempStaticRightsSubset)>0);
	SubsetDestroy(cIMMeasures, vTempStaticRightsSubset);
Endif;
SubsetCreate(cIMMeasures, vTempStaticRightsSubset);
SubsetElementInsert(cIMMeasures, vTempStaticRightsSubset, 'StaticRights', 1);

#zeroOut all related cell values related to StaticRights in the intermediate cube
IF (pIncremental @<> 'Y');

	vZerooutView = 'temp_IMCube_zeroout_' | pAppId;
	ViewCreate(cIMRDCLSCube, vZerooutView);
	ViewTitleDimensionSet(cIMRDCLSCube, vZerooutView, cIMMeasures);
	ViewSubsetAssign(cIMRDCLSCube, vZerooutView, cIMMeasures, vTempStaticRightsSubset);
	ViewZeroOut(cIMRDCLSCube, vZerooutView);
	ViewDestroy(cIMRDCLSCube, vZerooutView);
Endif;

vTotalGroups = DIMSIZ(cGroupsDim);
vGroupLooper = 1;
While (vGroupLooper <= vTotalGroups);
	vGroup = DIMNM(cGroupsDim, vGroupLooper);
	
	#zero out intermediate cell security cube based on this group and StaticRights fields only
	IF (pIncremental @= 'Y');
		#create a group subset that contains this group only
		vTempGroupSubset = 'temp_group_' | pAppId;
		vGroupsControlDim = '}Groups';
		IF (SubsetExists(vGroupsControlDim, vTempGroupSubset)>0);
			SubsetDestroy(vGroupsControlDim, vTempGroupSubset);
		Endif;
		SubsetCreate(vGroupsControlDim, vTempGroupSubset);
		SubsetElementInsert(vGroupsControlDim, vTempGroupSubset, vGroup, 1);

		vGroupView = 'tp_temp_group_view_' | pExecutionId;
		ViewCreate(cIMRDCLSCube, vGroupView);
		ViewTitleDimensionSet(cIMRDCLSCube, vGroupView, vGroupsControlDim);
		ViewSubsetAssign(cIMRDCLSCube, vGroupView, vGroupsControlDim, vTempGroupSubset );
		ViewTitleDimensionSet(cIMRDCLSCube, vGroupView,cIMMeasures);
		ViewSubsetAssign(cIMRDCLSCube, vGroupView, cIMMeasures, vTempStaticRightsSubset );
		ViewZeroOut(cIMRDCLSCube, vGroupView);
		ViewDestroy(cIMRDCLSCube, vGroupView);

		IF (SubsetExists(vGroupsControlDim, vTempGroupSubset)>0);
			SubsetDestroy(vGroupsControlDim, vTempGroupSubset);
		Endif;
	Endif;

	vTotalNodes = DIMSIZ(pApprovalDim);
	vNodeLooper = 1;
	While (vNodeLooper <= vTotalNodes);
		vApprovalNode = DIMNM(pApprovalDim, vNodeLooper);
		vFinalCellSecurity ='NONE';

		IF (versionIndex >0);
			vTotalVersions = DIMSIZ(pVersionDim);
			vVersionLooper = 1;
			While (vVersionLooper <= vTotalVersions);
				vVersion = DIMNM(pVersionDim, vVersionLooper);
				IF (approvalIndex >0)	;	
					IF (versionFirst @= 'Y');
						vFinalCellSecurity=CellGetS(cIMRDCLSCube, vVersion, vApprovalNode,cAllApplications, vGroup,'Rights' );
						vCurrentCellSecurity = CellGetS(cIMRDCLSCube, vVersion, vApprovalNode,cAllApplications, vGroup,'StaticRights');
						IF (vFinalCellSecurity @<> vCurrentCellSecurity);
							CellPutS(vFinalCellSecurity, cIMRDCLSCube, vVersion, vApprovalNode,cAllApplications, vGroup,'StaticRights');
						Endif;
						
					Else;
						vFinalCellSecurity=CellGetS(cIMRDCLSCube, vApprovalNode, vVersion,cAllApplications, vGroup,'Rights' );
						vCurrentCellSecurity = CellGetS(cIMRDCLSCube, vApprovalNode, vVersion,cAllApplications, vGroup,'StaticRights');
						IF (vFinalCellSecurity @<> vCurrentCellSecurity);
							CellPutS(vFinalCellSecurity, cIMRDCLSCube, vApprovalNode, vVersion,cAllApplications, vGroup,'StaticRights');
						Endif;
					Endif;
				Else;
					vFinalCellSecurity=CellGetS(cIMRDCLSCube,vVersion, cAllApplications, vGroup,'Rights' );
					vCurrentCellSecurity = CellGetS(cIMRDCLSCube,vVersion, cAllApplications, vGroup,'StaticRights' );
					IF (vFinalCellSecurity @<>vCurrentCellSecurity);
						CellPutS(vFinalCellSecurity, cIMRDCLSCube,vVersion, cAllApplications, vGroup,'StaticRights');
					Endif;
				Endif;
				vVersionLooper = vVersionLooper +1;
			End;

		ElseIf (versionIndex = 0 & approvalIndex >0);
				vFinalCellSecurity=CellGetS(cIMRDCLSCube,vApprovalNode, cAllApplications, vGroup,'Rights' );
				vCurrentCellSecurity = CellGetS(cIMRDCLSCube,vApprovalNode, cAllApplications, vGroup,'StaticRights');
				IF (vFinalCellSecurity @<>vCurrentCellSecurity);
					CellPutS(vFinalCellSecurity, cIMRDCLSCube,vApprovalNode, cAllApplications, vGroup,'StaticRights');
				Endif;
		Endif;
		vNodeLooper = vNodeLooper  +1;		
	End;
	vGroupLooper = vGroupLooper +1;
End;


IF (SubsetExists(cIMMeasures, vTempStaticRightsSubset)>0);
	SubsetDestroy(cIMMeasures, vTempStaticRightsSubset);
Endif;
#endregion